//--------------------------------------------------------------------------------------------------
//= SSB ComboCounter Module
//--------------------------------------------------------------------------------------------------
//	SSB ComboCounter でのみ使用するオリジナル命令 / 関数モジュール
//--------------------------------------------------------------------------------------------------
	#module _SSBComboCounterModule_

		//------------------------------------------------------------------------------------------
		//= コンボカウンタースキンを読み込む
		//------------------------------------------------------------------------------------------
		//　スキンの読み込みに成功した場合は1 をstatに返す。
		//------------------------------------------------------------------------------------------
			#deffunc LoadComboCounterSkin str _path
				ret = 0
				GselMem = ginfo_sel
				//スキンファイルとアルファマップファイル名
				skin	= ""+_path
				alpha	= ""+getpath(skin, 1)+ALPHA_MAP_FILE_PREFIX@+getpath(skin, 2)
				//アルファマップファイルが存在するか確認
				exist ""+alpha
				if( strsize != -1 ){
					AlphaMapFlag = TRUE@
				} else {
					AlphaMapFlag = FALSE@
				}
				//スキンファイルが存在するか確認
				exist ""+skin
				if( strsize != -1 ){
					buffer SCR_SKIN_CHECK@: picload ""+skin
					w = ginfo_winx: h = ginfo_winy
					//サイズが適切であれば読み込む
					if( w == CC_SKIN_SIZE_W@ )&&( h == CC_SKIN_SIZE_H@ ){
						celload ""+skin, SCR_CCFONT@
						celdiv SCR_CCFONT@, CCFONT_SIZE_W@, CCFONT_SIZE_H@
						//アルファマップ
						if( AlphaMapFlag == TRUE@ ){
							celload ""+alpha, SCR_CCFONT_ALPHA@
						} else {
							buffer SCR_CCFONT_ALPHA@, CC_SKIN_SIZE_W@, CC_SKIN_SIZE_H@: cls 4
							for i, 0, CC_SKIN_SIZE_W@, 1
								for j, 0, CC_SKIN_SIZE_H@, 1
									gsel SCR_CCFONT@
									pget i, j
									if( ginfo_r+ginfo_g+ginfo_b != 0 ){
										gsel SCR_CCFONT_ALPHA@
										color 255, 255, 255
										pset i, j
									}
								next
							next
						}
						celdiv SCR_CCFONT_ALPHA@, CCFONT_SIZE_W@, CCFONT_SIZE_H@
						ComboCounterSkinPath@ = skin
						ret = 1
					} else {
						Titlebar = ""+PROGRAM_NAME@
						Message = "スキン画像の解像度が適切ではありません。\n解像度は"+CC_SKIN_SIZE_W@+"x"+CC_SKIN_SIZE_H@+"である必要があります。\n"
						dialog Message, 0, Titlebar
					}
				} else {
					Titlebar = ""+PROGRAM_NAME@
					Message = "指定されたスキン画像ファイルが存在しないか、または無効なファイルです。"
					dialog Message, 0, Titlebar
				}
				gsel GselMem
			return ret

		//----------------------------------------------------------------------------------------------
		//= CCDraw	コンボカウンターを描画する
		//----------------------------------------------------------------------------------------------
		//	ComboCounterの内容をCCFontでカレントポジションに描画する。
		//	0〜99まで対応
		//----------------------------------------------------------------------------------------------
			#deffunc CCDraw int _cc, int _towid, int _ccwid
				CC		= _cc		//描画するコンボ数
				ToWID	= _towid	//描画先ウィンドウID
				CCWID	= _ccwid	//コンボカウンター画像のウィンドウID
				
				Text = str(limit(CC, 0, 99))
				TextLength = strlen(text)
				//コンボが終了した場合はカウンターを暗く
				if( ComboCounterBuf@(cnt@) < CCSTART@ ){
					shadow = 11
				} else {
					shadow = 0
				}
				//描画開始
				x = ginfo_cx	:	y = ginfo_cy
				GselMem = ginfo_sel
				gsel ToWID
				repeat TextLength
					//1桁の場合は右寄せにする
					if( TextLength == 1 ){
						x + CCFONT_SIZE_W@
					}
					pos x, y
					put = int(strmid(Text, cnt, 1))
					color ALPHA_COLOR@
					gmode 4, , , 255
					celput CCWID, put+shadow
					x + CCFONT_SIZE_W@
				loop
				pos x, y
				gcopy CCWID, CCFONT_SIZE_W@*10, CCFONT_SIZE_H@*(shadow != 0), CC_HITCOMBO_SIZE_W@, CC_HITCOMBO_SIZE_H@
				gsel GselMem
			return

		//------------------------------------------------------------------------------------------
		//= Exist関数
		//------------------------------------------------------------------------------------------
			#defcfunc ExistB str _path
				ret = 0
				exist _path
				if( strsize != -1 ){ ret = 1 }
			return ret
		
	#global