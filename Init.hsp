//--------------------------------------------------------------------------------------------------
//= Init.hsp
//--------------------------------------------------------------------------------------------------
//	ウィンドウ / バッファ / コンパイルオプションなどの初期化
//--------------------------------------------------------------------------------------------------

	//----------------------------------------------------------------------------------------------
	//= コンパイルオプション
	//----------------------------------------------------------------------------------------------
		#packopt hide 1

	//----------------------------------------------------------------------------------------------
	//= 終了処理ラベルを設定
	//----------------------------------------------------------------------------------------------
		onexit gosub *__end

	//----------------------------------------------------------------------------------------------
	//= 設定ファイルをロード
	//----------------------------------------------------------------------------------------------
		//存在しなかった場合初期設定をする
		if( ExistB(INI_FILE_NAME) == FALSE ){
			//PJ64のパスを求める
			Titlebar = ""+PROGRAM_NAME+" 初期設定"
			Message = {"初期設定を行います。
						Project64k または Project64KVE があるフォルダを
						選択してください。"}
			dialog Message, 0, Titlebar
			//ディレクトリ選択
			repeat
				tmp = ""
				selfolder tmp, "Project64k または Project64KVE があるフォルダを選択してください。"
				if( stat == 0 ){
					//PJ/KVEを探す なければ終了(どちらもある場合は若い方が優先される)
					ExistFileName = "Project64k.exe", "Project64KVE.exe"
					if( ExistB(""+tmp+"\\"+ExistFileName(0)) == 1 ){
						Project64kDirectoryPath = tmp
						break
					} else {
						if( ExistB(""+tmp+"\\"+ExistFileName(1)) == 1 ){
							Project64kDirectoryPath = tmp
							break
						} else {
							dialog "["+ExistFileName(0)+"]または["+ExistFileName(1)+"]が見つかりません。", 1, ""+PROGRAM_NAME
						}
					}
				} else {
					Titlebar = "Error"
					Message = {"ディレクトリが選択されませんでした。
								アプリケーションを終了します。"}
					dialog Message, 1, Titlebar
					end
				}
			loop
			//まずファイルを空で作成
			tmp = ""
			notesel tmp
			notesave ""+INI_FILE_NAME
			//セーブ
			gosub *__DataSave
		}
		//読み込み
		gosub *__DataLoad

	//----------------------------------------------------------------------------------------------
	//= コンボカウンターフォントバッファ初期化
	//----------------------------------------------------------------------------------------------
		//ファイルが存在するか確認
		exist ""+ComboCounterSkinPath
		if( strsize == -1 ){
			//スキンファイルが存在しなかった場合デフォルトに戻す
			ComboCounterSkinPath = ""+DEFAULT_COMBO_COUNTER_SKIN_PATH
		}
		LoadComboCounterSkin ComboCounterSkinPath

	//----------------------------------------------------------------------------------------------
	//= PJを実行
	//----------------------------------------------------------------------------------------------
		//PJ/KVEを探す なければ終了(どちらもある場合は若い方が優先される)
		ExistFileName = "Project64k.exe", "Project64KVE.exe"
		if( ExistB(""+Project64kDirectoryPath+"\\"+ExistFileName(0)) == 1 ){
			PJFileName = ExistFileName(0)
		} else {
			if( ExistB(""+Project64kDirectoryPath+"\\"+ExistFileName(1)) == 1 ){
				PJFileName = ExistFileName(1)
			} else {
				dialog "["+ExistFileName(0)+"]または["+ExistFileName(1)+"]が見つかりません。", 1, ""+PROGRAM_NAME
				end
			}
		}
		//PJを実行する
		DirMem = dir_cur
		chdir ""+Project64kDirectoryPath
		PJProcessID = PIDExec(""+PJFileName)
		//PJのウィンドウハンドルを取得
		if( PJFileName == ExistFileName(0) ){
			ClassName = "Project64k Version 0.13 Core 1.4":	WindowName = "Project64k Version 0.13 Core 1.4"
		} else {
			ClassName = "Project64KVE 0.13 Core 1.4      ":	WindowName = "Project64KVE 0.13 Core 1.4      "
		}
		FindWindow varptr(ClassName), varptr(WindowName)
		PJWindowHandle = stat
		chdir DirMem

	//----------------------------------------------------------------------------------------------
	//= ベースアドレスの取得
	//----------------------------------------------------------------------------------------------
		dim BaseAddress
		ReadProcMemory PJProcessID, BASE_ADDRESS_P, BaseAddress, 4

	//----------------------------------------------------------------------------------------------
	//= 下書きウィンドウ初期化
	//----------------------------------------------------------------------------------------------
		buffer SCR_DRAFT,		DRAFT_WINDOW_SIZE_W, DRAFT_WINDOW_SIZE_H
		buffer SCR_DRAFT_ALPHA,	DRAFT_WINDOW_SIZE_W, DRAFT_WINDOW_SIZE_H

	//----------------------------------------------------------------------------------------------
	//= OPSウィンドウ初期化
	//----------------------------------------------------------------------------------------------
		buffer	SCR_OPS_ALPHA,	GameScreenW, GameScreenH
		bgscr	SCR_OPS,		GameScreenW, GameScreenH
		enableAlphaBlendWindow SCR_OPS_ALPHA	//メインウィンドウのおブジェクト表示バグ回避
		gsel SCR_OPS, 2

	//----------------------------------------------------------------------------------------------
	//= メインウィンドウ初期化
	//----------------------------------------------------------------------------------------------
		screen SCR_MAIN, MAIN_WINDOW_SIZE_W, MAIN_WINDOW_SIZE_H
		title ""+PROGRAM_NAME+" "+PROGRAM_VERSION
		syscolor 15: boxf
		gosub *__SettingWindowObjectInit